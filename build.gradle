plugins {
    id "idea"
    id "java-library"
    id "maven-publish"
    id "groovy"
    id "signing"
    id "com.github.ben-manes.versions" version "$outdatedVersion"
}

group "be.looorent"
version "1.0.0"

sourceCompatibility = JavaVersion.VERSION_1_7

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    testImplementation (
            "org.codehaus.groovy:groovy-all:$groovyVersion",
            "org.junit.jupiter:junit-jupiter-api:$junitVersion",
            "org.junit.vintage:junit-vintage-engine:$junitVersion",
            "org.spockframework:spock-core:$spockVersion"
    )
}

compileJava {
    exclude "module-info.java"
    options.compilerArgs = ["--release", "7"]
}

test {
    useJUnitPlatform()
}

task compileModuleInfoJava(type: JavaCompile) {
    classpath = files()
    source = "src/main/java/module-info.java"
    destinationDir = compileJava.destinationDir
    doFirst {
        options.compilerArgs = [
                "--release", "9",
                "--module-path", compileJava.classpath.asPath,
        ]
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

compileModuleInfoJava.dependsOn compileJava
classes.dependsOn compileModuleInfoJava

def sonatypeUsername = hasProperty("ossrhUser") ? ossrhUser : System.getenv("ossrhUser")
def sonatypePassword = hasProperty("ossrhPassword") ? ossrhPassword : System.getenv("ossrhPassword")

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "anpr-wiegand"
            from components.java
            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
            pom {
                name = project.name
                description = "Formats license plates to Wiegand formats"
                url = "https://github.com/looorent/anpr-wiegand"
                licenses {
                    license {
                        name = "The MIT License (MIT)"
                        url = "http://opensource.org/licenses/MIT"
                    }
                }
                developers {
                    developer {
                        id = "looorent"
                        name = "Lorent Lempereur"
                        email = "lorent.lempereur.dev@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/looorent/anpr-wiegand"
                    developerConnection = "scm:git:git@github.com:looorent/anpr-wiegand.git"
                    url = "https://github.com/looorent/anpr-wiegand"
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            allowInsecureProtocol=true
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    doFirst {
        options.addStringOption("Xdoclint:none", "-quiet")
    }
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}